apply plugin: 'com.android.application'

def getVersion() {
    def cmd = 'git describe --tags'
    Process process = cmd.execute()
    String[] descriptionInfo = process.in.text.split('-')
    return descriptionInfo[0] + "." + descriptionInfo[1]
}

def keystorePropertiesFile = rootProject.file("keys.properties")//данные для подписи сертификата
def keystoreProperties = new Properties()// достаем ключи из файла, который нах. на компе локально
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.darya.myapplication"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 1 // показывает разницу между версиями (нужна для плэй маркета)
        versionName getVersion()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {// ключи для подписи (подписываем наше приложения с помощью данных)
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    android.applicationVariants.all { variant ->// генерируем называние exe
        variant.outputs.all {
            outputFileName = "${rootProject.name}-${variant.versionName}.apk"
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version" //то, по чему мы группируем наши версия
            versionNameSuffix "-dev"
        }
        master {
            dimension "version"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
//    implementation 'com.google.android.material:material:1.1.0-alpha01'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    def nav_version = "1.0.0-alpha07"
    //noinspection GradleDependency
    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    //noinspection GradleDependency
    implementation "android.arch.navigation:navigation-ui:$nav_version"

    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.sufficientlysecure:html-textview:3.6'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
}
